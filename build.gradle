// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

android {
    namespace 'ru.evotor.integrations'

    def version = 22

    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 23
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode version
        versionName "0.6.03"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resValue "integer", "versionCodeIntegrationLibrary", "$version"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/*_module'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.annotation:annotation:1.4.0'

    api 'com.github.evotor:push-notifications:v0.2.1'
    api 'com.github.evotor:query-api:1.2.0'

    implementation 'com.google.code.gson:gson:2.8.9'

    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    google()
}

buildscript {

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.7.10"
    }
}
// in the individual module build.gradle files

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

dokkaJavadoc.configure {
    moduleName = 'integration-library'

    outputDirectory.set(file("$buildDir/javadoc"))

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/dokkaHtml"))

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            groupId 'com.github.evotor'
            artifactId "integration-library"
            version "STDEV-13837"
            artifact(sourceJar)
            artifact(javadocJar)
            artifact "${project.buildDir}/outputs/aar/${project.archivesBaseName}-release.aar"

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    if (dependency.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}
