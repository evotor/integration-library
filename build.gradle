// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

android {
    def version = 11

    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 25
        versionCode version
        versionName "0.4.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "integer", "versionCodeIntegrationLibrary", "$version"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
//    compile 'com.android.support:support-annotations:28.0.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile 'com.github.evotor:push-notifications:v0.2.1'
    compile 'com.github.evotor:query-api:1.0.0'
    compile 'com.google.code.gson:gson:2.8.5'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
    google()
}

buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17"
    }
}
// in the individual module build.gradle files

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

dokka {
    moduleName = 'integration-library'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"

    includes = ['packages.md']

}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"

    includes = ['packages.md']
}

task dokkaHTMLAsJava(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "html-as-java"
    outputDirectory = "$buildDir/dokkaHtmlasjava"

    includes = ['packages.md']
}

task dokkaHTML(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "html"
    outputDirectory = "$buildDir/dokkaHtml"

    includes = ['packages.md']
}

task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

publishing {
    publications {

        mavenKotlin(MavenPublication) {
            groupId 'com.github.evotor'
            artifactId "integration-library"
            version "STDEV-1901"
            artifact(sourceJar)
            artifact(javadocJar)
            artifact "${project.buildDir}/outputs/aar/${project.archivesBaseName}-release.aar"

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    if (dependency.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}
